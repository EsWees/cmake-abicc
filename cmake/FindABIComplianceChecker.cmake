# Setting following variables:
# CTAGS_PATH
# ABICC_EXECUTABLE
# ABICC_COMPILER_PATH
# ABICC_CROSS_PREFIX
# ABICC_LDCONFIG_DIR
# ABICC_VERSION_STRING

IF(NOT CTAGS_PATH)
	FIND_PROGRAM(CTAGS_EXECUTABLE
		ctags
	DOC
		"ctags tool path")
	MARK_AS_ADVANCED(CTAGS_EXECUTABLE)

	IF(CTAGS_EXECUTABLE)
		GET_FILENAME_COMPONENT(CTAGS_PATH "${CTAGS_EXECUTABLE}" DIRECTORY)
	ENDIF()
ENDIF()

FIND_PROGRAM(ABICC_EXECUTABLE
	abi-compliance-checker
DOC
	"ABI Compliance Checker tool path")
MARK_AS_ADVANCED(ABICC_EXECUTABLE)

IF(ABICC_EXECUTABLE)
	EXECUTE_PROCESS(COMMAND ${ABICC_EXECUTABLE} -dumpversion
	OUTPUT_VARIABLE
		ABICC_VERSION_STRING
	ERROR_QUIET
	OUTPUT_STRIP_TRAILING_WHITESPACE)
ENDIF()

IF(NOT ABICC_COMPILER_PATH)
	GET_FILENAME_COMPONENT(ABICC_COMPILER_PATH
			       "${CMAKE_C_COMPILER}" DIRECTORY)
ENDIF()

IF(NOT ABICC_CROSS_PREFIX)
	EXECUTE_PROCESS(COMMAND
		${CMAKE_C_COMPILER} -dumpmachine
	OUTPUT_VARIABLE
		ABICC_CROSS_PREFIX
	ERROR_VARIABLE
		RET
	OUTPUT_STRIP_TRAILING_WHITESPACE)
	IF(RET)
		UNSET(ABICC_CROSS_PREFIX)
	ENDIF()
ENDIF()

IF(NOT ABICC_LDCONFIG_DIR)
	FIND_PROGRAM(LDCONFIG_EXECUTABLE
		ldconfig
	PATHS
		"${ABICC_COMPILER_PATH}"
		"${ABICC_COMPILER_PATH}/../../.." # workaround for Yocto
	PATH_SUFFIXES
		bin
		sbin
	NO_DEFAULT_PATH)
	MARK_AS_ADVANCED(LDCONFIG_EXECUTABLE)
	IF(LDCONFIG_EXECUTABLE)
		GET_FILENAME_COMPONENT(ABICC_LDCONFIG_DIR
			"${LDCONFIG_EXECUTABLE}" DIRECTORY)
	ENDIF()
ENDIF()

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(ABIComplianceChecker
REQUIRED_VARS
	CTAGS_PATH
	ABICC_EXECUTABLE
	ABICC_COMPILER_PATH
	ABICC_CROSS_PREFIX
	ABICC_LDCONFIG_DIR
VERSION_VAR
	ABICC_VERSION_STRING)
